module Main exposing (..)

import Browser
import Html exposing (Html, button, div, text)
import Html.Attributes exposing (class, classList)
import Html.Events exposing (onClick)
import Random
import Random.List exposing (shuffle)
import Time


type CardValue
    = Ace
    | Two
    | Three
    | Four
    | Five
    | Six
    | Seven
    | Eight
    | Nine
    | Ten
    | Jack
    | Queen
    | King


type Suit
    = Spades
    | Hearts
    | Diamonds
    | Clubs


type alias Card =
    { value : CardValue
    , suit : Suit
    , face : String
    }


type alias SelectedCards =
    ( Maybe Card, Maybe Card )


deck : List Card
deck =
    [ { value = Ace, suit = Spades, face = "ðŸ‚¡" }
    , { value = Two, suit = Spades, face = "ðŸ‚¢" }
    , { value = Three, suit = Spades, face = "ðŸ‚£" }
    , { value = Four, suit = Spades, face = "ðŸ‚¤" }
    , { value = Five, suit = Spades, face = "ðŸ‚¥" }
    , { value = Six, suit = Spades, face = "ðŸ‚¦" }
    , { value = Seven, suit = Spades, face = "ðŸ‚§" }
    , { value = Eight, suit = Spades, face = "ðŸ‚¨" }
    , { value = Nine, suit = Spades, face = "ðŸ‚©" }
    , { value = Ten, suit = Spades, face = "ðŸ‚ª" }
    , { value = Jack, suit = Spades, face = "ðŸ‚«" }
    , { value = Queen, suit = Spades, face = "ðŸ‚­" }
    , { value = King, suit = Spades, face = "ðŸ‚®" }
    , { value = Ace, suit = Hearts, face = "ðŸ‚±" }
    , { value = Two, suit = Hearts, face = "ðŸ‚²" }
    , { value = Three, suit = Hearts, face = "ðŸ‚³" }
    , { value = Four, suit = Hearts, face = "ðŸ‚´" }
    , { value = Five, suit = Hearts, face = "ðŸ‚µ" }
    , { value = Six, suit = Hearts, face = "ðŸ‚¶" }
    , { value = Seven, suit = Hearts, face = "ðŸ‚·" }
    , { value = Eight, suit = Hearts, face = "ðŸ‚¸" }
    , { value = Nine, suit = Hearts, face = "ðŸ‚¹" }
    , { value = Ten, suit = Hearts, face = "ðŸ‚º" }
    , { value = Jack, suit = Hearts, face = "ðŸ‚»" }
    , { value = Queen, suit = Hearts, face = "ðŸ‚½" }
    , { value = King, suit = Hearts, face = "ðŸ‚¾" }
    , { value = Ace, suit = Clubs, face = "ðŸƒ" }
    , { value = Two, suit = Clubs, face = "ðŸƒ‚" }
    , { value = Three, suit = Clubs, face = "ðŸƒƒ" }
    , { value = Four, suit = Clubs, face = "ðŸƒ„" }
    , { value = Five, suit = Clubs, face = "ðŸƒ…" }
    , { value = Six, suit = Clubs, face = "ðŸƒ†" }
    , { value = Seven, suit = Clubs, face = "ðŸƒ‡" }
    , { value = Eight, suit = Clubs, face = "ðŸƒˆ" }
    , { value = Nine, suit = Clubs, face = "ðŸƒ‰" }
    , { value = Ten, suit = Clubs, face = "ðŸƒŠ" }
    , { value = Jack, suit = Clubs, face = "ðŸƒ‹" }
    , { value = Queen, suit = Clubs, face = "ðŸƒ" }
    , { value = King, suit = Clubs, face = "ðŸƒŽ" }
    , { value = Ace, suit = Diamonds, face = "ðŸƒ‘" }
    , { value = Two, suit = Diamonds, face = "ðŸƒ’" }
    , { value = Three, suit = Diamonds, face = "ðŸƒ“" }
    , { value = Four, suit = Diamonds, face = "ðŸƒ”" }
    , { value = Five, suit = Diamonds, face = "ðŸƒ•" }
    , { value = Six, suit = Diamonds, face = "ðŸƒ–" }
    , { value = Seven, suit = Diamonds, face = "ðŸƒ—" }
    , { value = Eight, suit = Diamonds, face = "ðŸƒ˜" }
    , { value = Nine, suit = Diamonds, face = "ðŸƒ™" }
    , { value = Ten, suit = Diamonds, face = "ðŸƒš" }
    , { value = Jack, suit = Diamonds, face = "ðŸƒ›" }
    , { value = Queen, suit = Diamonds, face = "ðŸƒ" }
    , { value = King, suit = Diamonds, face = "ðŸƒž" }
    ]


cardBack : String
cardBack =
    "ðŸ‚ "



-- MODEL


type alias Model =
    { cards : List Card
    , selectedCards : SelectedCards
    , matchedCards : List Card
    , timer : Int
    , gameStarted : Bool
    }


model : Model
model =
    { cards = deck
    , selectedCards = ( Nothing, Nothing )
    , matchedCards = []
    , timer = 0
    , gameStarted = False
    }



-- UPDATE


type Msg
    = Shuffle
    | ShuffledList (List Card)
    | SelectCard Card
    | IncrementTimer Time.Posix
    | ResetGame
    | StartGame


update : Msg -> Model -> ( Model, Cmd Msg )
update msg currModel =
    case msg of
        Shuffle ->
            ( currModel, Random.generate ShuffledList (shuffle model.cards) )

        ShuffledList shuffledList ->
            ( { currModel | cards = shuffledList }, Cmd.none )

        SelectCard selectedCard ->
            case currModel.selectedCards of
                ( Nothing, Nothing ) ->
                    ( { currModel | selectedCards = ( Just selectedCard, Nothing ) }, Cmd.none )

                ( Just _, Just _ ) ->
                    ( { currModel | selectedCards = ( Just selectedCard, Nothing ) }, Cmd.none )

                ( Just c, Nothing ) ->
                    ( { currModel
                        | selectedCards =
                            if List.member selectedCard currModel.matchedCards || (c == selectedCard) then
                                ( Just c, Nothing )

                            else
                                ( Just c, Just selectedCard )
                        , matchedCards =
                            if isPair c selectedCard && not (c == selectedCard) then
                                c :: selectedCard :: currModel.matchedCards

                            else
                                currModel.matchedCards
                      }
                    , Cmd.none
                    )

                -- TODO: Possibly handle differently as should never occur
                ( Nothing, Just c ) ->
                    ( { currModel | selectedCards = ( Just selectedCard, Just c ) }, Cmd.none )

        IncrementTimer _ ->
            ( { currModel | timer = currModel.timer + 1 }, Cmd.none )

        ResetGame ->
            ( model, Cmd.none )

        StartGame ->
            ( { currModel | gameStarted = True }, Random.generate ShuffledList (shuffle currModel.cards) )


isPair : Card -> Card -> Bool
isPair c1 c2 =
    if c1.value == c2.value then
        case c1.suit of
            Spades ->
                c2.suit == Spades || c2.suit == Clubs

            Clubs ->
                c2.suit == Spades || c2.suit == Clubs

            Hearts ->
                c2.suit == Hearts || c2.suit == Diamonds

            Diamonds ->
                c2.suit == Hearts || c2.suit == Diamonds

    else
        False



-- VIEW


view : Model -> Html Msg
view m =
    div []
        [ div [] [ text <| String.fromInt <| m.timer ]
        , div [ class "cards" ] (List.map (viewCard m) m.cards)
        , div []
            [ button [ onClick ResetGame ] [ text "Reset" ]
            , button [ onClick StartGame ] [ text "Start" ]
            ]
        ]


viewCard : Model -> Card -> Html Msg
viewCard m c =
    div
        [ classList
            [ ( "card", True )
            , ( "red", (c.suit == Hearts || c.suit == Diamonds) && (isSelected c m.selectedCards || List.member c m.matchedCards) )
            , ( "back", not (isSelected c m.selectedCards) )
            ]
        , onClick (SelectCard c)
        ]
        [ text
            (if isSelected c m.selectedCards || List.member c m.matchedCards then
                c.face

             else
                cardBack
            )
        ]


isSelected : Card -> SelectedCards -> Bool
isSelected c currentSelected =
    case currentSelected of
        ( Nothing, Just sc ) ->
            sc == c

        ( Just sc, Nothing ) ->
            sc == c

        ( Just sc1, Just sc2 ) ->
            (sc1 == c) || (sc2 == c)

        ( Nothing, Nothing ) ->
            False



-- Subscriptions


subscriptions : Model -> Sub Msg
subscriptions m =
    if m.gameStarted && (List.length m.matchedCards < 52) then
        Time.every 1000 IncrementTimer

    else
        Sub.none


main : Program () Model Msg
main =
    Browser.element
        { init = \_ -> ( model, Cmd.none )
        , view = view
        , update = update
        , subscriptions = subscriptions
        }
